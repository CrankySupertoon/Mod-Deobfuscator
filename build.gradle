plugins {
  id 'com.github.ben-manes.versions' version '0.25.0'
  id 'java'
  id 'maven'
  id 'maven-publish'
  id 'eclipse'
  id 'idea'
  id 'com.github.johnrengelman.shadow' version '5.1.0'
}

version = '2.5.0'
group = 'com.crankysupertoon.moddeobf'
targetCompatibility = sourceCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven{ url = 'https://files.minecraftforge.net/maven/'}
}

dependencies {
	compile 'com.google.code.gson:gson:2.8.5'
    compile 'net.sf.jopt-simple:jopt-simple:5.0.4'
    compile 'net.minecraftforge:srgutils:0.2.12'
    compile 'org.ow2.asm:asm:7.2'
    compile 'org.ow2.asm:asm-commons:7.2'
    compile 'org.ow2.asm:asm-tree:7.2'
}

task copyAndReplace(type: Copy) {
    from(sourceSets.main.java) {
        include '**/ModDeobf.java'
        expand 'DEV': project.version
    }
    from(sourceSets.main.java) {
        exclude '**/ModDeobf.java'
    }
    into project.getBuildDir().toString() + "/sources/java"
}
tasks.compileJava.dependsOn copyAndReplace
tasks.compileJava.setSource(project.getBuildDir().toString() + '/sources/java')

def MANIFEST = manifest {
    attributes([
        'Main-Class': 'com.crankysupertoon.moddeobf.ModDeobf',
        'Implementation-Title': 'Mod Deobfuscator',
        'Implementation-Version': project.version,
        'Built-With': 'Gradle v' + project.getGradle().getGradleVersion()
    ] as LinkedHashMap)
}

jar {
    manifest.from(MANIFEST)
}

shadowJar {
    classifier 'all'
    manifest.from(MANIFEST)
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest.from(MANIFEST)
}

artifacts {
    archives shadowJar
    archives jar
    archives sourcesJar
}
